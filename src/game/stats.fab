vars /game
    U level_number
    UU[7] piece_counts_bcd = UU[7](0)
    UUU score_bcd = 0
    UU lines_bcd = 0
    // UU[4] line_clears_bcd = UU[4](0)
    U pushdown = 0

fn increment_piece_count(U piece_id)
    U piece_type = piece_id_to_type_table[piece_id]
    UU count = piece_counts_bcd[piece_type]
    count += 1
    if count.a & %1111 >= $0A
        count.a += 6
    if count.a >= $A0
        count.a += $60
        count.b += 1
    piece_counts_bcd[piece_type] = count
    game_render_flags |= GAME_RENDER_PIECE_COUNT

fn handle_line_count()
    if completed_lines == 0
        return
    game_render_flags |= GAME_RENDER_LINES
    if game_type == GAME_TYPE_A
        lines_bcd.a += completed_lines
        if lines_bcd.a & %1111 >= $0A
            lines_bcd.a += 6
        if lines_bcd.a >= $A0
            lines_bcd.a += $60
            lines_bcd.b += 1
        if should_transition()
            level_number += 1
            game_render_flags |= GAME_RENDER_LEVEL
            set_sfx(SFX_LEVEL_UP)
    else
        lines_bcd.a -= completed_lines
        if S(lines_bcd.a) < 0
            lines_bcd = 0
        else if lines_bcd.a & %1111 >= $0A
            lines_bcd -= 6

// performs carries in the way that the game handles them
fn carry_bcd_uuu(UUU bcd) UUU
    if bcd.a & %1111 >= $0A
        bcd.a += $06
    if bcd.a >= $A0
        bcd.a += $60
        bcd.b += 1
    if bcd.b & %1111 >= $0A
        bcd.b += $06
    if bcd.b >= $A0
        bcd.b += $60
        bcd.c += 1
    if bcd.c & %1111 >= $0A
        bcd.c += $06
    return bcd

// TODO: this is wrong???
fn handle_pushdown()
    if S(pushdown) < 2
        return
    score_bcd += pushdown - 1
    // carry logic
    if score_bcd.a & %1111 >= $0A
        score_bcd.a += $06
    if score_bcd.a >= $A0
        score_bcd.a &= $F0
        score_bcd.a += $60
        score_bcd.b += 1
    game_render_flags |= GAME_RENDER_SCORE
    pushdown = 0

fn handle_line_clear_points()
    UU points = points_bcd_table[completed_lines]
    for U i = 0; i <= level_number; i += 1
        score_bcd += points
        score_bcd = carry_bcd_uuu(score_bcd)
        if score_bcd.c >= $A0
            score_bcd = $999999
            break
    game_render_flags |= GAME_RENDER_SCORE
    completed_lines = 0

fn should_transition() Bool
    // bugged level comparison from the original game
    U false_level = (lines_bcd.b << 4) + (lines_bcd.a >> 4)
    if VERSION == VERSION_NWC
        false_level <<= 1
        if lines_bcd.a & %1111 >= 5
            false_level += 1
    // TODO: verify that this replicates the long level
    return S(level_number) < false_level

ct U[256] legacy_level_display_table = U[](
    $00,$01,$02,$03,$04,$05,$06,$07,$08,$09,$10,$11,$12,$13,$14,$15,
    $16,$17,$18,$19,$20,$21,$22,$23,$24,$25,$26,$27,$28,$29,$00,$0A,
    $14,$1E,$28,$32,$3C,$46,$50,$5A,$64,$6E,$78,$82,$8C,$96,$A0,$AA,
    $B4,$BE,$C6,$20,$E6,$20,$06,$21,$26,$21,$46,$21,$66,$21,$86,$21,
    $A6,$21,$C6,$21,$E6,$21,$06,$22,$26,$22,$46,$22,$66,$22,$86,$22,
    $A6,$22,$C6,$22,$E6,$22,$06,$23,$26,$23,$85,$A8,$29,$F0,$4A,$4A,
    $4A,$4A,$8D,$07,$20,$A5,$A8,$29,$0F,$8D,$07,$20,$60,$A6,$49,$E0,
    $15,$10,$53,$BD,$D6,$96,$A8,$8A,$0A,$AA,$E8,$BD,$EA,$96,$8D,$06,
    $20,$CA,$A5,$BE,$C9,$01,$F0,$1E,$A5,$B9,$C9,$05,$F0,$0C,$BD,$EA,
    $96,$38,$E9,$02,$8D,$06,$20,$4C,$67,$97,$BD,$EA,$96,$18,$69,$0C,
    $8D,$06,$20,$4C,$67,$97,$BD,$EA,$96,$18,$69,$06,$8D,$06,$20,$A2,
    $0A,$B1,$B8,$8D,$07,$20,$C8,$CA,$D0,$F7,$E6,$49,$A5,$49,$C9,$14,
    $30,$04,$A9,$20,$85,$49,$60,$A5,$B1,$29,$03,$D0,$78,$A9,$00,$85,
    $AA,$A6,$AA,$B5,$4A,$F0,$5C,$0A,$A8,$B9,$EA,$96,$85,$A8,$A5,$BE,
    $C9,$01,$D0,$0A,$A5,$A8,$18,$69,$06,$85,$A8,$4C,$BD,$97,$A5,$B9,
    $C9,$04,$D0,$0A,$A5,$A8,$38,$E9,$02,$85,$A8,$4C,$BD,$97,$A5,$A8,
)
